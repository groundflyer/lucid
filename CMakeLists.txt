cmake_minimum_required(VERSION 3.9)

project(lucid CXX)

# Compiler with C++17 support is required
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Check some C++17 features
include(cmake/Checks.cmake)

# External dependencies
# Threads
set(CMAKE_THREAD_PREFER_PTHREAD 1)
set(THREADS_PREFER_PTHREAD_FLAG 1)
find_package(Threads REQUIRED)

# OpenGL
find_package(OpenGL REQUIRED)

# GLFW3
find_package(glfw3 3.2 REQUIRED)

# Default Build Type is Release
if(NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
  set(CMAKE_BUILD_TYPE Release CACHE STRING "Choose the type of build." FORCE)
  set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "Debug" "Release" "RelWithDebInfo")
endif()
message(STATUS "Build Type: ${CMAKE_BUILD_TYPE}")

# Options
if(CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
  option(USE_LIBCXX "Use LibC++ when compiling with clang" OFF)
endif()
option(USE_DOUBLE_PRECISION "Use double precision floating-point format" OFF)
option(BUILD_TESTS "Build tests" OFF)

# Use GCC/Clang pipe
add_compile_options(-Wall -Wpedantic -Wextra -pipe)

# Optimize for the host machine
add_compile_options($<$<CONFIG:Release>:-O3>)
add_compile_options($<$<CONFIG:Release>:-DNDEBUG>)
add_compile_options($<$<CONFIG:Release>:-march=native>)
add_compile_options($<$<CONFIG:Release>:-ffast-math>)

# Link-time optimization
if(CMAKE_BUILD_TYPE STREQUAL "Release")
  include(CheckIPOSupported)
  check_ipo_supported(RESULT HAS_IPO OUTPUT IPO_ERROR LANGUAGES CXX)
  if(HAS_IPO)
    option(ENABLE_IPO "Enable interprocedural optimization" OFF)
    if(ENABLE_IPO)
      message(STATUS "Interprocedural optimization enabled")
      set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ON)
    endif()
  else()
    message(STATUS "Interprocedural optimization is not supported: ${IPO_ERROR}")
  endif()
endif()

# Don't optimize Debug build
add_compile_options($<$<CONFIG:Debug>:-O0>)
add_compile_options($<$<CONFIG:Debug>:-g>)

# RelWithDebInfo
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "")
add_compile_options($<$<CONFIG:RelWithDebInfo>:-O3>)
add_compile_options($<$<CONFIG:RelWithDebInfo>:-g>)
add_compile_options($<$<CONFIG:RelWithDebInfo>:-march=native>)
add_compile_options($<$<CONFIG:RelWithDebInfo>:-ffast-math>)

# Colored output from Clang
add_compile_options($<$<CXX_COMPILER_ID:Clang>:-fcolor-diagnostics>)

# Print vectorization statistics
if(CMAKE_VERBOSE_MAKEFILE)
  add_compile_options($<$<CXX_COMPILER_ID:GNU>:-fopt-info-vec>)
  add_compile_options($<$<CXX_COMPILER_ID:Clang>:-Rpass=loop-vectorize>)
endif()

# Set floating point precision
set(_precision "single")
if(USE_DOUBLE_PRECISION)
  set(_precision "double")
  add_definitions(-D_DOUBLE_PRECISION)
endif()
message(STATUS "Floating-point format: ${_precision} precision")

# spdlog
include_directories(${CMAKE_CURRENT_SOURCE_DIR}/spdlog/include)

add_subdirectory(src)
